# SPDX-License-Identifier: AGPL-3.0-or-later

module PartiduoUi
  # Coquille de l'écran (maquette de référence, ADR-005 D5) : barre supérieure
  # (dossier, exercice et période, recherche, langue, utilisateur) et menu
  # latéral par domaine. Tout vient de `Partiduo::Api` : le menu de
  # l'utilisateur (`Api::Modules.menu`, modules actifs et permissions), la
  # société (`Api::Core.settings`), la session (`Api::Auth.session`).
  class Shell
    include Marten::Template::Object::Auto

    # Une entrée du menu latéral. `url` nil : écran pas encore livré par
    # l'interface (entrée affichée, désactivée). `badge` : code d'extension ;
    # `count` : compteur d'une extension (`Extensions.counter`), affiché s'il
    # est positif.
    class Item
      include Marten::Template::Object::Auto

      getter code : String
      getter label_key : String
      getter url : String?
      getter active : Bool # ameba:disable Naming/QueryBoolMethods
      getter badge : String?
      getter count : Int64?

      def initialize(@code, @label_key, @url, @active, @badge, count : Int64? = nil)
        @count = count.try { |value| value > 0 ? value : nil }
      end

      def disabled : Bool
        url.nil?
      end
    end

    # Une rubrique : libellé (`nil` pour une entrée de premier niveau sans
    # rubrique, comme le tableau de bord) et ses entrées.
    class Section
      include Marten::Template::Object::Auto

      getter code : String
      getter label_key : String?
      getter items : Array(Item)

      def initialize(@code, @label_key, @items)
      end
    end

    getter sections : Array(Section)
    getter company_name : String?
    getter company_detail : String?
    getter user_name : String
    getter user_email : String
    getter user_initials : String
    getter user_role_key : String
    getter locale : String
    getter locales : Array(String)
    getter current_path : String
    getter fiscal_year : String?
    getter period : String?
    getter period_groups : Array(PeriodGroup)?
    # Mode simplifié de la micro-entreprise (`SimpleMode`, ADR-007 D3) ;
    # `mode_target` : mode proposé au comptable (`simple` ou `full`) ;
    # `settings_url` : paramètres de la micro-entreprise, dans le menu de
    # l'utilisateur en mode simplifié.
    property simple : Bool = false
    property mode_target : String? = nil
    property settings_url : String? = nil

    # Une période proposée dans la barre supérieure.
    class PeriodOption
      include Marten::Template::Object::Auto

      getter id : Int64
      getter label : String
      getter selected : Bool

      def initialize(@id, @label, @selected)
      end
    end

    # Les périodes d'un exercice (`<optgroup>`).
    class PeriodGroup
      include Marten::Template::Object::Auto

      getter label : String
      getter options : Array(PeriodOption)

      def initialize(@label, @options)
      end
    end

    # Cookie de la période choisie dans la barre supérieure.
    PERIOD_COOKIE = "partiduo_period"

    def initialize(@sections, @company_name, @company_detail, @user_name, @user_email, @user_role_key,
                   @locale, @locales, @current_path, @fiscal_year = nil, @period = nil, @period_groups = nil)
      @user_initials = initials(@user_name.presence || @user_email)
    end

    # Période de travail : celle choisie dans la barre supérieure (cookie),
    # sinon la période courante du socle (`Api::Core.current_period`).
    def self.working_period(request : Marten::HTTP::Request, actor : Partiduo::Api::Actor) : Partiduo::Api::Core::PeriodView?
      if chosen = request.cookies[PERIOD_COOKIE]?.try(&.to_i64?)
        begin
          return Partiduo::Api::Core.period(actor, chosen)
        rescue Partiduo::Api::NotFound
        end
      end
      Partiduo::Api::Core.current_period(actor)
    end

    def self.build(request : Marten::HTTP::Request, current : Current, format : Format = Format.new(I18n.locale)) : Shell
      actor = current.actor
      session = current.session!
      settings = begin
        Partiduo::Api::Core.settings(actor)
      rescue Partiduo::Api::NotFound | Partiduo::Api::AccessDenied # session sous le niveau exigé
        nil
      end
      detail = settings.try do |values|
        parts = [] of String
        parts << "SIREN #{values.siren}" unless values.siren.empty?
        parts << values.vat_number if values.siren.empty? && !values.vat_number.empty?
        parts << I18n.t("ui.shell.regime", regime: values.tax_regime.upcase) unless values.tax_regime.empty?
        parts.join(" · ").presence
      end

      period = period_values(request, actor, format)
      simple = SimpleMode.enabled?(request)
      menu = Partiduo::Api::Modules.menu(actor)
      counts = Extensions.counts(actor)
      shell = new(
        sections: simple ? SimpleMode.sections(menu, request.path, counts) : sections(menu, extension_codes(actor), request.path, counts),
        company_name: settings.try(&.company_name.presence),
        company_detail: detail,
        user_name: session.full_name,
        user_email: session.email,
        user_role_key: "auth.roles.#{session.role}",
        locale: I18n.locale,
        locales: Locale.available,
        current_path: request.full_path,
        fiscal_year: period[:fiscal_year],
        period: period[:period],
        period_groups: period[:period_groups],
      )
      shell.simple = simple
      shell.mode_target = SimpleMode.switch_target(request)
      shell.settings_url = resolve("micro:settings") if simple && actor.can?("micro.settings.write")
      shell
    end

    # Exercice et période de travail, et les périodes des exercices
    # ouverts pour le choix dans la barre supérieure (ADR-005 D5).
    private def self.period_values(request : Marten::HTTP::Request, actor : Partiduo::Api::Actor, format : Format)
      working = begin
        working_period(request, actor)
      rescue Partiduo::Api::AccessDenied
        return {fiscal_year: nil.as(String?), period: nil.as(String?), period_groups: nil.as(Array(PeriodGroup)?)}
      end
      groups = Partiduo::Api::Core.fiscal_years(actor).reject(&.closed?).compact_map do |year|
        next if year.periods.empty?
        PeriodGroup.new(year.label, year.periods.map do |item|
          label = format.period(item.starts_on, item.ends_on)
          label = I18n.t("ui.shell.period_closed", period: label) if item.closed?
          PeriodOption.new(item.id, label, item.id == working.try(&.id))
        end)
      end
      if working && groups.none?(&.options.any?(&.selected))
        groups.unshift(PeriodGroup.new(working.fiscal_year_label, [PeriodOption.new(working.id, format.period(working.starts_on, working.ends_on), true)]))
      end
      {
        fiscal_year:   working.try(&.fiscal_year_label).as(String?),
        period:        working.try { |item| format.period(item.starts_on, item.ends_on) }.as(String?),
        period_groups: (groups.empty? ? nil : groups).as(Array(PeriodGroup)?),
      }
    end

    # Rubriques du menu : d'abord les domaines, puis les extensions (ADR-005 D5).
    def self.sections(menu : Array(Partiduo::Api::Modules::MenuView), extensions : Set(String), path : String,
                      counts : Hash(String, Int64) = {} of String => Int64) : Array(Section)
      menu.compact_map do |entry|
        if entry.children.empty?
          url = resolve(entry.route)
          Section.new(entry.code, nil, [Item.new(entry.code, entry.label_key, url, active?(url, path),
            badge(entry, extensions), counts[entry.code]?)])
        else
          items = entry.children.map do |child|
            url = resolve(child.route)
            Item.new(child.code, child.label_key, url, active?(url, path), badge(child, extensions), counts[child.code]?)
          end
          Section.new(entry.code, entry.label_key, items)
        end
      end
    end

    # URL d'un nom de route du manifeste ; `nil` si l'interface ne le
    # fournit pas encore (ou s'il attend des paramètres).
    def self.resolve(route : String?) : String?
      return if route.nil?
      Marten.routes.reverse(route)
    rescue Marten::Routing::Errors::NoReverseMatch
      nil
    end

    def self.active?(url : String?, path : String) : Bool
      return false if url.nil?
      url == "/" ? path == "/" : (path == url || path.starts_with?(url.rstrip('/') + "/"))
    end

    private def self.badge(entry : Partiduo::Api::Modules::MenuView, extensions : Set(String)) : String?
      extensions.includes?(entry.module_code) ? entry.module_code : nil
    end

    private def self.extension_codes(actor : Partiduo::Api::Actor) : Set(String)
      Partiduo::Api::Modules.list(actor).select(&.kind.==("extension")).map(&.code).to_set
    end

    private def initials(name : String) : String
      words = name.split(/[\s@._-]+/).reject(&.empty?)
      letters = words.size > 1 ? [words.first, words.last] : words.first(1)
      letters.map(&.[0].upcase).join
    end
  end
end
